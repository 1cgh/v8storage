#Использовать "../.."
#Использовать asserts
#Использовать tempfiles
#Использовать fs

// Реализация шагов BDD-фич/сценариев c помощью фреймворка https://github.com/artbear/1bdd

Перем БДД; //контекст фреймворка 1bdd

// Метод выдает список шагов, реализованных в данном файле-шагов
Функция ПолучитьСписокШагов(КонтекстФреймворкаBDD) Экспорт
	БДД = КонтекстФреймворкаBDD;

	ВсеШаги = Новый Массив;

	ВсеШаги.Добавить("ЯЗагружаюФайлКонфигурацииВБазуДанных");
	ВсеШаги.Добавить("ЯСоздаюФайловоеХранилищеСПараметромПодключенияБазыКХранилищу");
	ВсеШаги.Добавить("ВыводЛогаСодержит");
	ВсеШаги.Добавить("ЯВыполняюОтключениеОтХранилищаКонфигурации");
	ВсеШаги.Добавить("ЯПодключаюБазуКХранилищуСПараметромЗаменыКонфигурации");
	ВсеШаги.Добавить("ЯВыполняюСборкуОбработкиКонвертацииОтчетаИзXmlВJsonИСохраняюВПеременной");
	ВсеШаги.Добавить("ФайлИзПеременнойСуществует");
	ВсеШаги.Добавить("ЯОбновлениеКонфигурацииИзХранилищаНаВерсию");

	Возврат ВсеШаги;
КонецФункции

// Реализация шагов

// Процедура выполняется перед запуском каждого сценария
Процедура ПередЗапускомСценария(Знач Узел) Экспорт

КонецПроцедуры

// Процедура выполняется после завершения каждого сценария
Процедура ПослеЗапускаСценария(Знач Узел) Экспорт
	//ВременныеФайлы.Удалить();
КонецПроцедуры

//Я загружаю файл конфигурации "../tests/fixtures/1Cv8.cf" в базу данных
Процедура ЯЗагружаюФайлКонфигурацииВБазуДанных(Знач ПутьКФайлуКонфигурации) Экспорт

	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	УправлениеКонфигуратором = ХранилищеКонфигурации.ПолучитьУправлениеКонфигуратором();

	УправлениеКонфигуратором.ЗагрузитьКонфигурациюИзФайла(ПутьКФайлуКонфигурации);

КонецПроцедуры

//Я создаю файловое хранилище с параметром подключения базы к хранилищу "Истина"
Процедура ЯСоздаюФайловоеХранилищеСПараметромПодключенияБазыКхранилищу(Знач ПодключитьсяКхранилищу) Экспорт

	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ХранилищеКонфигурации.СоздатьХранилищеКонфигурации(ПодключитьсяКхранилищу);

КонецПроцедуры

//Вывод лога содержит "Хранилище конфигурации создано"
Процедура ВыводЛогаСодержит(Знач СодержаниеВыводаКоманды) Экспорт
	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ВыводКоманды = ХранилищеКонфигурации.ПолучитьВыводКоманды();
	Ожидаем.Что(ВыводКоманды, "Вывод команды не содержит требуемой строки").Содержит(СодержаниеВыводаКоманды);
КонецПроцедуры

//Я выполняю отключение от хранилища конфигурации
Процедура ЯВыполняюОтключениеОтХранилищаКонфигурации() Экспорт

	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ХранилищеКонфигурации.ОтключитьсяОтХранилища();

КонецПроцедуры

//Я подключаю базу к хранилищу с параметром замены конфигурации "Истина"
Процедура ЯПодключаюБазуКхранилищуСПараметромЗаменыКонфигурации(Знач ПодключитьсяКхранилищу) Экспорт

	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ХранилищеКонфигурации.ПодключитьсяКХранилищу(Истина, ПодключитьсяКхранилищу);

КонецПроцедуры

//Я выполняю сборку обработки конвертации отчета из xml в json и сохраняю в переменной "ОбработкаXMLJSON"
Процедура ЯВыполняюСборкуОбработкиКонвертацииОтчетаИзXmlВJsonИСохраняюВПеременной(Знач ИмяПеременной) Экспорт
	
	ИмяВременногоФайла = ВременныеФайлы.НовоеИмяФайла("epf");
	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ХранилищеКонфигурации.СобратьОбработкуКонвертации(КаталогОбработки(), ИмяВременногоФайла);

	БДД.СохранитьВКонтекст(ИмяПеременной, ИмяВременногоФайла);

КонецПроцедуры

//Файл из переменной "ОбарботкаXMLJSON" существует
Процедура ФайлИзПеременнойСуществует(Знач ИмяПеременной) Экспорт
	ВременныйФайл = БДД.ПолучитьИзКонтекста(ИмяПеременной);
    Ожидаем.Что(Новый Файл(ВременныйФайл), "Файл не существует").Существует();
КонецПроцедуры

//Я обновление конфигурации из хранилища на версию "2"
Процедура ЯОбновлениеКонфигурацииИзХранилищаНаВерсию(Знач НомерВерсии) Экспорт

	ХранилищеКонфигурации = БДД.ПолучитьИзКонтекста("ХранилищеКонфигурации");
	ХранилищеКонфигурации.ОбновитьКонфигурациюНаВерсию(НомерВерсии);

КонецПроцедуры


Функция КаталогОбработки()
	
	Возврат ОбъединитьПути(КаталогПроекта(), "epf", "ОбработкаКонвертацииMXLJSON", "ОбработкаКонвертацииMXLJSON.xml");

КонецФункции

Функция КаталогПроекта()

	Возврат ОбъединитьПути(ТекущийСценарий().Каталог, "..", "..");
	
КонецФункции
