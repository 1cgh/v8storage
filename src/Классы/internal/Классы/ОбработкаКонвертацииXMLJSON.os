Перем ХранилищеКонфигурации;
Перем ПутьКФайлуОбработку;
Перем Лог;
Перем ЭтоПриложениеEXE;

Функция ПолучитьПутьКОбработке() Экспорт
	
	СобратьОбработку();

	Возврат ПутьКФайлуОбработку;

КонецФункции

Процедура СобратьОбработку()

	ФайлОбработки = Новый Файл(ПутьКФайлуОбработку);

	Если ФайлОбработки.Существует() Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоСборкаEXE() Тогда
		ЗагрузчикВнешнейEPF = Новый ЗагрузчикВнешнейEPF;
		ПутьКФайлуОбработку = ЗагрузчикВнешнейEPF.ПолучитьПутьКФайлу();
	Иначе

		ПутьКФайлуОбработку = ПутьКСобраннойEpf();

		Если ПустаяСтрока(ПутьКФайлуОбработку) Тогда
			Лог.Отладка("Не найдена готовая обработка. Делаю попытку собрать временную обработку исходников");
		
			ПутьКФайлуОбработку = СобратьФайлОбработки();

		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

Функция ПутьКСобраннойEpf()
	
	КаталогБиблиотеки = ОбъединитьПути(ОбъединитьПути(ТекущийСценарий().Каталог, "..", "..", ".."), "..");

	ФайлКаталогаБиблиотеки = Новый Файл(КаталогБиблиотеки);

	ПутьККаталогуБиблиотеки = ФайлКаталогаБиблиотеки.ПолноеИмя;

	ПутьКСобраннойEpf = ОбъединитьПути(ПутьККаталогуБиблиотеки, "bin", "ОбработкаКонвертацииMXLJSON.epf");

	ФайлСобраннойEpf = Новый Файл(ПутьКСобраннойEpf);

	Если ФайлСобраннойEpf.Существует() Тогда
		Лог.Отладка("Найдена готовая обработка по пути: %1", ПутьКСобраннойEpf );
			
		Возврат ФайлСобраннойEpf.ПолноеИмя;
	КонецЕсли;

	Возврат "";

КонецФункции

Функция СобратьФайлОбработки()
	
	ПутьКНовомуФайлу = ПолучитьПутьКФайлу("ОбработкаКонвертацииXMLJSON.epf", "1.0.0");

	КаталоИсходниковОбработки = ОбъединитьПути(ТекущийСценарий().Каталог, "epf");
	ФайлXmlОбработки = ОбъединитьПути(КаталоИсходниковОбработки, "ОбработкаКонвертацииMXLJSON.xml");

	ХранилищеКонфигурации.СобратьОбработкуКонвертации(КаталоИсходниковОбработки, ФайлXmlОбработки, ПутьКНовомуФайлу);

	Возврат ПутьКНовомуФайлу;

КонецФункции
	
Функция ПолучитьПутьКФайлу(ИмяФайла, ВерсияФайла)
	ПутьКФайлу = ОбъединитьПути(КаталогВременныхФайлов(), ".v8storage", СтрЗаменить(ВерсияФайла, ".", "_"), ИмяФайла);
	Возврат ПутьКФайлу;
КонецФункции

Функция ЭтоСборкаEXE() Экспорт
	
	Если ЭтоПриложениеEXE = Неопределено Тогда
		ЭтоПриложениеEXE = ВРег(Прав(ТекущийСценарий().Источник, 3)) = "EXE";
	КонецЕсли;

	Возврат ЭтоПриложениеEXE;

КонецФункции

Процедура ПриСозданииОбъекта(Знач ЗначениеХранилищеКонфигурации)
	
	ХранилищеКонфигурации = ЗначениеХранилищеКонфигурации;

	ПутьКФайлуОбработку = "";

	Лог = Логирование.ПолучитьЛог("oscript.lib.v8storage");

КонецПроцедуры