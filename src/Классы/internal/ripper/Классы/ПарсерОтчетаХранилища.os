#Использовать datetime

// Выполняет чтение файла отчета в таблицу версий
//
// Параметры:
//   ПутьКФайлу - Строка - путь к файлу отчета из хранилища
//
// Возвращаемое значение:
//   ТаблицаЗначений - колонки:
//      Номер   - число - номер версии
//      Дата    - Дата - Дата версии
//      Автор   - строка - автор версии
//      Комментарий   - Строка - многострочная строка с комментарием к версии
//
Функция ПрочитатьФайлОтчетаХранилища(Знач ПутьКФайлу) Экспорт
	
	ТекстФайла = ПрочитатьФайл(ПутьКФайлу);

	Возврат ПрочитатьТекстСкобкоФайла(ТекстФайла);

КонецФункции

Функция ПрочитатьТекстСкобкоФайла(Текст)
	
	ЧитательСкобкоФайла = Новый ripper;

	НачальныйМассив = ЧитательСкобкоФайла.Разобрать(Текст);

	ПослеОбхода = РекурсивныйОбход(НачальныйМассив[0]);
	
	ТаблицаВерсий = СформироватьТаблицуВерсий(ПослеОбхода);
	
	Возврат ТаблицаВерсий;

КонецФункции

Функция ПрочитатьФайл(Знач ПутьКФайлу) 
	
	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Возврат ТекстФайла;

КонецФункции


Функция РекурсивныйОбход(Элемент);

	Если ТипЗнч(Элемент) = Тип("Массив") Тогда
		
		НовыйМассив = Новый Массив;
		Для Каждого Стр Из Элемент Цикл
			
			ЭначениеСтр = РекурсивныйОбход(Стр);
			
			Если ЭначениеСтр = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
		    НовыйМассив.Добавить(РекурсивныйОбход(Стр));
			
		КонецЦикла; 
		
		Если НовыйМассив.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если НовыйМассив.Количество() = 1 Тогда
			Возврат НовыйМассив[0];
		КонецЕсли;

		
		Возврат НовыйМассив;
		
	ИначеЕсли ТипЗнч(Элемент) = Тип("Соответствие")
		ИЛИ ТипЗнч(Элемент) = Тип("Структура") Тогда
		
		Для Каждого Стр Из Элемент Цикл
			
			Стр.Значение = РекурсивныйОбход(Стр.Значение);
			
		КонецЦикла; 
		
		Возврат Элемент;
		
		
	ИначеЕсли Не СтрНачинаетсяС(Элемент, """") Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат Элемент;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТаблицуВерсий()
	
    ТаблицаВерсий = Новый ТаблицаЗначений;
    ТаблицаВерсий.Колонки.Добавить("Номер");
    ТаблицаВерсий.Колонки.Добавить("Дата");
    ТаблицаВерсий.Колонки.Добавить("Автор");
    ТаблицаВерсий.Колонки.Добавить("Комментарий");

	Возврат ТаблицаВерсий;

КонецФункции

Функция СформироватьТаблицуВерсий(Массив) 
	
	ТаблицуВерсий = ПолучитьТаблицуВерсий();
	Версия = Неопределено;
	
	Для ИИ = 0 По Массив.ВГраница() Цикл
		
		Стр = Массив[ИИ];
		
		Если НЕ ТипЗнч(Стр) = Тип("Массив") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Стр.Количество() > 2 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстЭлемента = УбратьКавычки(Стр[1]);
		                  		
		Если СтрНачинаетсяС(ТекстЭлемента, "Версия:") Тогда
			
			Версия = ТаблицуВерсий.Добавить();
		
			ИИ = ИИ + 1;
			
			Версия.Номер = УбратьКавычки(Массив[ИИ][1]);
			
		ИначеЕсли СтрНачинаетсяС(ТекстЭлемента, "Пользователь:") Тогда
			ИИ = ИИ + 1;
			Версия.Автор = УбратьКавычки(Массив[ИИ][1]);
			
		ИначеЕсли СтрНачинаетсяС(ТекстЭлемента, "Дата создания:") Тогда
			ИИ = ИИ + 1;
			Версия.Дата = УбратьКавычки(Массив[ИИ][1]);
	
		ИначеЕсли СтрНачинаетсяС(ТекстЭлемента, "Время создания:") Тогда
			ИИ = ИИ + 1;
			Версия.Дата = СтрШаблон("%1 %2", Версия.Дата, УбратьКавычки(Массив[ИИ][1]));

		ИначеЕсли СтрНачинаетсяС(ТекстЭлемента, "Комментарий:") Тогда
			ИИ = ИИ + 1;
			Версия.Комментарий = УбратьКавычки(Массив[ИИ][1]);
		КонецЕсли;		

		
	КонецЦикла;
	
	Для Каждого ОписаниеВерсии Из ТаблицуВерсий Цикл
		
		ОписаниеВерсии.Номер = Число(ОписаниеВерсии.Номер);
		ОписаниеВерсии.Дата = РаботаСДатой.СтрокаВДату(ОписаниеВерсии.Дата, "dd.MM.yyyy HH:mm:ss");
		
	КонецЦикла;

	Возврат ТаблицуВерсий;
	
КонецФункции

Функция УбратьКавычки(Знач СтрокаДанных)
	
	Если СтрНачинаетсяС(СтрокаДанных, """") Тогда 
		
		СтрокаДанных = Сред(СтрокаДанных,2);
		
	КонецЕсли;	
	
	Если СтрЗаканчиваетсяНа(СтрокаДанных, """") Тогда 
		
		СтрокаДанных = Лев(СтрокаДанных, СтрДлина(СтрокаДанных) -1 );
		
	КонецЕсли;	
	
	Возврат СтрокаДанных;
	
КонецФункции

